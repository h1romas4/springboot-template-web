buildscript {
	repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
	}
	dependencies {
		classpath 'org.springframework.boot:spring-boot-gradle-plugin:2.0.3.RELEASE'
        classpath "com.moowork.gradle:gradle-node-plugin:1.2.0"
	}
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'com.moowork.node'
apply plugin: 'eclipse'

apply from:'ide-eclipse.gradle'

node {
    version = '8.11.3'
    download = true
}

task watch(dependsOn: ['npm_run_watch'])
task webpack(dependsOn: ['npm_run_webpack'])

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

def defaultEncoding = 'UTF-8'
[compileJava, compileTestJava]*.options*.encoding = defaultEncoding

// for DOMA2
processResources.destinationDir = compileJava.destinationDir
compileJava.dependsOn processResources

// for heroku
task stage(dependsOn: ['webpack', 'clean', 'build'])
build.mustRunAfter clean

bootRun {
    sourceResources sourceSets.main
}

repositories {
    jcenter()
}

dependencies {
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.springframework.boot:spring-boot-starter-thymeleaf')
    compile('org.seasar.doma.boot:doma-spring-boot-starter:1.1.1')
    compile('org.springframework.boot:spring-boot-devtools')
    runtime('org.flywaydb:flyway-core')
    runtime('com.h2database:h2:1.4.196')
    runtime('org.postgresql:postgresql:9.4-1201-jdbc4')
    annotationProcessor('org.seasar.doma.boot:doma-spring-boot-starter:1.1.1')
}
