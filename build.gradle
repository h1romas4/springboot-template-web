plugins {
	id 'org.springframework.boot' version '3.1.2'
    id "com.github.node-gradle.node" version "7.0.0"
    id "org.seasar.doma.compile" version "1.1.0"
}

apply plugin: 'java'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'com.github.node-gradle.node'
apply plugin: 'org.seasar.doma.compile'

/**
 * for JDK
 */
sourceCompatibility = '17'
targetCompatibility = '17'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

/**
 * for node.js & webpack
 */
node {
    version = '18.17.1'
    download = true
}

task webpack(dependsOn: ['npm_run_webpack'])
task debug(dependsOn: ['npm_run_debug'])
task watch(dependsOn: ['npm_run_watch'])
task server(dependsOn: ['npm_run_server'])

/**
 * for DOMA2
 */
apply from:'ide.gradle'

compileTestJava {
    options.compilerArgs = ['-proc:none']
}

bootJar {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

jar {
    enabled = false
}

/**
 * clean build
 */
task cleanGen(type: Delete) {
    delete fileTree('src/main/resources/static') {
        include 'css/*.*'
        include 'res/*.*'
        include 'js/*.*'
    }
}

// build order
webpack.dependsOn cleanGen
webpack.mustRunAfter cleanGen
build.dependsOn webpack
build.mustRunAfter webpack
bootRun.dependsOn debug
bootRun.mustRunAfter webpack

/**
 * for heroku deploy
 */
task stage(dependsOn: ['clean', 'build']) {
    mustRunAfter 'clean'
}

/**
 * for Spring Boot
 */
bootRun {
    // for static resource hot reloading
    //  https://docs.spring.io/spring-boot/docs/current/gradle-plugin/reference/html/#running-your-application-reloading-resources
    sourceResources sourceSets.main
    // remote debug support @see .vscode/launch.json
    jvmArgs=["-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=7778"]
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity6'
    implementation 'org.seasar.doma.boot:doma-spring-boot-starter:1.7.0'
    implementation 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'org.flywaydb:flyway-core:9.21.1'
    runtimeOnly 'com.h2database:h2:2.2.220'
    runtimeOnly 'org.postgresql:postgresql:42.6.0'
    annotationProcessor 'org.seasar.doma:doma-processor:2.53.1'
}
